#include <Arduino.h>
#include <WiFi.h>
#include <PubSubClient.h>
#include <WiFiClientSecure.h>
#include <ArduinoJson.h>
#include <WiFiManager.h>

//==== MQTT settings ====
const char* mqtt_server = "49b6dcd6236247be8bcfe1416017e3b6.s1.eu.hivemq.cloud";
const char* mqtt_username = "group15_iot";
const char* mqtt_password = "Group15@iot";
const int mqtt_port = 8883;
const char* id = "67e31110a0e08c0712e1db36";

WiFiClientSecure espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
const char* topic = "LED";
const char* re_topic = "LED/receive";
static const char* root_ca PROGMEM = R"EOF(
-----BEGIN CERTIFICATE-----
MIIFazCCA1OgAwIBAgIRAIIQz7DSQONZRGPgu2OCiwAwDQYJKoZIhvcNAQELBQAw
TzELMAkGA1UEBhMCVVMxKTAnBgNVBAoTIEludGVybmV0IFNlY3VyaXR5IFJlc2Vh
cmNoIEdyb3VwMRUwEwYDVQQDEwxJU1JHIFJvb3QgWDEwHhcNMTUwNjA0MTEwNDM4
WhcNMzUwNjA0MTEwNDM4WjBPMQswCQYDVQQGEwJVUzEpMCcGA1UEChMgSW50ZXJu
ZXQgU2VjdXJpdHkgUmVzZWFyY2ggR3JvdXAxFTATBgNVBAMTDElTUkcgUm9vdCBY
MTCCAiIwDQYJKoZIhvcNAQEBBQADggIPADCCAgoCggIBAK3oJHP0FDfzm54rVygc
h77ct984kIxuPOZXoHj3dcKi/vVqbvYATyjb3miGbESTtrFj/RQSa78f0uoxmyF+
0TM8ukj13Xnfs7j/EvEhmkvBioZxaUpmZmyPfjxwv60pIgbz5MDmgK7iS4+3mX6U
A5/TR5d8mUgjU+g4rk8Kb4Mu0UlXjIB0ttov0DiNewNwIRt18jA8+o+u3dpjq+sW
T8KOEUt+zwvo/7V3LvSye0rgTBIlDHCNAymg4VMk7BPZ7hm/ELNKjD+Jo2FR3qyH
B5T0Y3HsLuJvW5iB4YlcNHlsdu87kGJ55tukmi8mxdAQ4Q7e2RCOFvu396j3x+UC
B5iPNgiV5+I3lg02dZ77DnKxHZu8A/lJBdiB3QW0KtZB6awBdpUKD9jf1b0SHzUv
KBds0pjBqAlkd25HN7rOrFleaJ1/ctaJxQZBKT5ZPt0m9STJEadao0xAH0ahmbWn
OlFuhjuefXKnEgV4We0+UXgVCwOPjdAvBbI+e0ocS3MFEvzG6uBQE3xDk3SzynTn
jh8BCNAw1FtxNrQHusEwMFxIt4I7mKZ9YIqioymCzLq9gwQbooMDQaHWBfEbwrbw
qHyGO0aoSCqI3Haadr8faqU9GY/rOPNk3sgrDQoo//fb4hVC1CLQJ13hef4Y53CI
rU7m2Ys6xt0nUW7/vGT1M0NPAgMBAAGjQjBAMA4GA1UdDwEB/wQEAwIBBjAPBgNV
HRMBAf8EBTADAQH/MB0GA1UdDgQWBBR5tFnme7bl5AFzgAiIyBpY9umbbjANBgkq
hkiG9w0BAQsFAAOCAgEAVR9YqbyyqFDQDLHYGmkgJykIrGF1XIpu+ILlaS/V9lZL
ubhzEFnTIZd+50xx+7LSYK05qAvqFyFWhfFQDlnrzuBZ6brJFe+GnY+EgPbk6ZGQ
3BebYhtF8GaV0nxvwuo77x/Py9auJ/GpsMiu/X1+mvoiBOv/2X/qkSsisRcOj/KK
NFtY2PwByVS5uCbMiogziUwthDyC3+6WVwW6LLv3xLfHTjuCvjHIInNzktHCgKQ5
ORAzI4JMPJ+GslWYHb4phowim57iaztXOoJwTdwJx4nLCgdNbOhdjsnvzqvHu7Ur
TkXWStAmzOVyyghqpZXjFaH3pO3JLF+l+/+sKAIuvtd7u+Nxe5AW0wdeRlN8NwdC
jNPElpzVmbUq4JUagEiuTDkHzsxHpFKVK7q4+63SM1N95R1NbdWhscdCb+ZAJzVc
oyi3B43njTOQ5yOf+1CceWxG1bQVs5ZufpsMljq4Ui0/1lvh+wjChP4kqKOJ2qxq
4RgqsahDYVvTH9w7jXbyLeiNdd8XM2w9U/t7y0Ff/9yi0GE44Za4rF2LN9d11TPA
mRGunUHBcnWEvgJBQl9nJEiU0Zsnvgc/ubhPgXRR4Xq37Z0j4r7g1SgEEzwxA57d
emyPxgcYxn/eR44/KJ4EBs+lVDR3veyJm+kXQ99b21/+jh5Xos1AnX5iItreGCc=
-----END CERTIFICATE-----
)EOF";

// GPIO setting
#define PIR_PIN 13  // Ch√¢n k·∫øt n·ªëi v·ªõi HC-SR501
#define LED_PIN 12  // Ch√¢n k·∫øt n·ªëi v·ªõi LED

bool is_control = false;   // LED c√≥ ƒëang ƒë∆∞·ª£c ƒëi·ªÅu khi·ªÉn?
bool motion = false;       // tr·∫°ng th√°i c·∫£m bi·∫øn
bool prev_motion = false;  // tr·∫°ng th√°i c≈© c·ªßa c·∫£m bi·∫øn

//==== Task Handles ====
TaskHandle_t SensorTaskHandle;
TaskHandle_t NetworkTaskHandle;


//==== ƒêi·ªÅu khi·ªÉn LED v√† c·∫£m bi·∫øn ====
void SensorTask(void* parameter) {
  while (1) {
    if (is_control) {
      vTaskDelay(pdMS_TO_TICKS(1000));
      continue;
    } else {
      motion = digitalRead(PIR_PIN);                                  // ƒê·ªçc tr·∫°ng th√°i c·∫£m bi·∫øn
      if (motion != prev_motion) xTaskNotifyGive(NetworkTaskHandle);  // G·ª≠i t√≠n hi·ªáu ƒë·∫øn NetworkTask khi c·∫£m bi·∫øn thay ƒë·ªïi
      prev_motion = motion;

      if (motion == HIGH) {
        digitalWrite(LED_PIN, HIGH);
        // Serial.println("Ph√°t hi·ªán chuy·ªÉn ƒë·ªông! LED s√°ng.");
      } else {
        digitalWrite(LED_PIN, LOW);
        // Serial.println("Kh√¥ng c√≥ chuy·ªÉn ƒë·ªông! LED t·∫Øt.");
      }

      vTaskDelay(pdMS_TO_TICKS(1000));
    }

    vTaskDelay(pdMS_TO_TICKS(500));
  }
}

//==== Duy tr√¨ k·∫øt n·ªëi WiFi v√† MQTT ====
void NetworkTask(void* parameter) {
  while (1) {
    if (WiFi.status() != WL_CONNECTED) {
      is_control = false; // T·∫Øt ch·∫ø ƒë·ªô ƒëi·ªÅu khi·ªÉn qua m·∫°ng
      Serial.println("WiFi disconnected! Reconnecting...");
      connectWiFi();
    }

    if (!client.connected()) {
      is_control = false; // T·∫Øt ch·∫ø ƒë·ªô ƒëi·ªÅu khi·ªÉn qua m·∫°ng
      Serial.println("MQTT disconnected! Reconnecting...");
      reconnect();
    }
    client.loop();

    // N·∫øu nh·∫≠n t√≠n hi·ªáu t·ª´ SensorTask, g·ª≠i MQTT
    if (ulTaskNotifyTake(pdTRUE, pdMS_TO_TICKS(1000))) {
      publishMessage(topic, true);
    }

    vTaskDelay(pdMS_TO_TICKS(1000));
  }
}

//==== K·∫øt n·ªëi WiFi ====
void connectWiFi() {
  WiFiManager wm;
  Serial.println("üîÑ ƒêang k·∫øt n·ªëi WiFi...");
  wm.setConfigPortalTimeout(10);  // ‚è≥ Gi·ªõi h·∫°n AP trong 10 gi√¢y

  if (WiFi.SSID() != "") {  
    // üîπ N·∫øu ƒë√£ c√≥ WiFi l∆∞u s·∫µn, th·ª≠ k·∫øt n·ªëi l·∫°i
    WiFi.begin();
    int retry_count = 0;
    while (WiFi.status() != WL_CONNECTED && retry_count < 15) {  
      Serial.print(".");
      vTaskDelay(pdMS_TO_TICKS(1000));
      retry_count++;
    }

    if (WiFi.status() == WL_CONNECTED) {
      Serial.println("\n‚úÖ WiFi ƒë√£ k·∫øt n·ªëi l·∫°i!");
      Serial.print("üì∂ ƒê·ªãa ch·ªâ IP: ");
      Serial.println(WiFi.localIP());
      return;
    } else {
      Serial.println("\n‚ö†Ô∏è Kh√¥ng k·∫øt n·ªëi l·∫°i ƒë∆∞·ª£c, chuy·ªÉn sang ch·∫ø ƒë·ªô AP.");
    }
  }

  // üîπ N·∫øu ch∆∞a c√≥ WiFi ho·∫∑c kh√¥ng k·∫øt n·ªëi l·∫°i ƒë∆∞·ª£c, v√†o ch·∫ø ƒë·ªô c·∫•u h√¨nh AP
  if (!wm.autoConnect("AutoLED_Config")) {  
    Serial.println("‚ùå Kh√¥ng th·ªÉ k·∫øt n·ªëi WiFi, ti·∫øp t·ª•c ch·∫°y ch∆∞∆°ng tr√¨nh...");
  } else {
    Serial.println("‚úÖ WiFi ƒë√£ k·∫øt n·ªëi qua AP Config!");
    Serial.print("üì∂ ƒê·ªãa ch·ªâ IP: ");
    Serial.println(WiFi.localIP());
  }
}

//==== K·∫øt n·ªëi MQTT ====
void reconnect() {
  int attempts = 0;
  while (!client.connected() && attempts < 10) {
    Serial.print("Connecting to MQTT...");
    String clientId = "ESP32Client-" + String(random(0xffff), HEX);

    if (client.connect(clientId.c_str(), mqtt_username, mqtt_password)) {
      Serial.println("Connected to MQTT");
      client.subscribe(re_topic);
      return;
    } else {
      Serial.print("Failed, rc=");
      Serial.println(client.state());
      vTaskDelay(pdMS_TO_TICKS(1000));
    }
    attempts++;
  }
}

//==== G·ª≠i d·ªØ li·ªáu MQTT ====
void publishMessage(const char* topic, boolean retained) {
  StaticJsonDocument<200> jsonDoc;
  jsonDoc["_id"] = id;
  JsonObject telemetry = jsonDoc.createNestedObject("telemetry");
  telemetry["state"] = motion;

  char jsonBuffer[200];
  serializeJson(jsonDoc, jsonBuffer);

  int attempts = 0;
  while (attempts < 10) {
    if (client.publish(topic, jsonBuffer, retained)) {
      Serial.println("Message published [" + String(topic) + "]: " + String(jsonBuffer));
      return;
    } else {
      attempts++;
      vTaskDelay(pdMS_TO_TICKS(1000));
    }
  }
  Serial.println("Failed to publish message");
}

//==== X·ª≠ l√Ω nh·∫≠n d·ªØ li·ªáu t·ª´ MQTT ====
void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived on topic [");
  Serial.print(topic);
  Serial.print("]: ");

  // Chuy·ªÉn payload th√†nh chu·ªói JSON
  char message[length + 1];
  memcpy(message, payload, length);
  message[length] = '\0';
  Serial.println(message);

  // Parse JSON
  StaticJsonDocument<200> jsonDoc;
  DeserializationError error = deserializeJson(jsonDoc, message);

  if (error) {
    Serial.print("JSON parsing failed: ");
    Serial.println(error.f_str());
    return;
  }

  // Ki·ªÉm tra deviceId c√≥ tr√πng kh·ªõp kh√¥ng
  const char* receivedId = jsonDoc["deviceId"];
  if (receivedId && strcmp(receivedId, id) == 0) {
    motion = jsonDoc["state"];
    Serial.print("Updated state: ");
    Serial.println(motion ? "LED ON" : "LED OFF");

    // ƒêi·ªÅu khi·ªÉn LED
    if (motion) {
      is_control = true;
      digitalWrite(LED_PIN, HIGH);
      Serial.println("Turn LED ON");
    } else {
      is_control = false;
      prev_motion = false;
      digitalWrite(LED_PIN, LOW);
      Serial.println("Turn LED OFF");
    }
  } else {
    Serial.println("Device ID mismatch, ignoring message.");
  }
}

void setup() {
  pinMode(PIR_PIN, INPUT);  // Ch√¢n c·∫£m bi·∫øn l√† INPUT
  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, LOW);
  Serial.begin(115200);
  Serial.println("Serial connected");

  connectWiFi();
  espClient.setCACert(root_ca);
  client.setServer(mqtt_server, mqtt_port);
  client.setCallback(callback);
  if (!client.connected()) reconnect();
  client.loop();

  // Kh·ªüi t·∫°o Task c·∫£m bi·∫øn tr√™n Core 0
  xTaskCreatePinnedToCore(SensorTask, "SensorTask", 4096, NULL, 1, &SensorTaskHandle, 0);

  // Kh·ªüi t·∫°o Task m·∫°ng tr√™n Core 1
  xTaskCreatePinnedToCore(NetworkTask, "NetworkTask", 4096, NULL, 1, &NetworkTaskHandle, 1);
}

void loop() {
  vTaskDelay(portMAX_DELAY);  // Loop kh√¥ng l√†m g√¨ v√¨ t·∫•t c·∫£ ch·∫°y trong Task
}